buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:4.0.4'
    }
}

plugins {
    id 'java'
}

apply plugin: 'de.undercouch.download'

def profilerSourceLocation = "$buildDir/resources/com/thelastpickle/tlpcluster/commands/origin/provisioning/cassandra"

task download(type: Download) {
    src 'https://github.com/jvm-profiling-tools/async-profiler/releases/download/v1.7/async-profiler-1.7-linux-x64.tar.gz'
    dest buildDir
    overwrite false
}

task untar(type: Copy) {
    from tarTree(resources.gzip("$buildDir/async-profiler-1.7-linux-x64.tar.gz"))
    into "$buildDir/profiler"
    dependsOn("download")
}

task copyWrappers(type: Copy) {
    from "src"
    into "$buildDir/profiler"
}

task createTarball(type: Tar) {
    archiveFileName = "profiler.tgz"
    compression = Compression.GZIP
    destinationDirectory = file(profilerSourceLocation)
    from "$buildDir/profiler/"
    dependsOn("copyWrappers")
}

sourceSets {
    main {
        output.dir("$buildDir/resources", builtBy: 'createTarball')
    }
}



